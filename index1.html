<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <title>Title</title>
    <style>
        body {
            margin: 0px;
            overflow: hidden;
        }
    </style>
</head>
<body bgcolor="#aaaaaa">

    <script type="text/javascript" src="./bigdata/js/libs/dat.gui.min.js"></script>
    <script type="text/javascript" src="./bigdata/js/jquery-3.4.1.min.js"></script>

    <!--
    <script type="x-shader/x-vertex" id="vertexShader">
        varying vec3 vWorldPosition;
        void main() {
            vec4 worldPosition = modelMatrix * vec4( position, 1.0 );
            vWorldPosition = worldPosition.xyz;
            gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
        }
    </script>
    <script type="x-shader/x-fragment" id="fragmentShader">
			uniform vec3 topColor;
			uniform vec3 bottomColor;
			uniform float offset;
			uniform float exponent;
			varying vec3 vWorldPosition;
			void main() {
				float h = normalize( vWorldPosition + offset ).y;
				gl_FragColor = vec4( mix( bottomColor, topColor, max( pow( max( h, 0.0 ), exponent ), 0.0 ) ), 1.0 );
			}
    </script>
    -->

    <script id="post-vert-2" type="x-shader/x-vertex">
    //摄像机景深效果
			varying vec2 vUv;

			void main() {
				vUv = uv;
				gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
			}
		</script>
    <script id="post-frag-2" type="x-shader/x-fragment">
			varying vec2 vUv;
			uniform sampler2D tColor;
			uniform float textureWidth;
			uniform float textureHeight;

			float getWeight(float dist, float maxDist){
				return 1.0 - dist/maxDist;
			}

			void main() {
				vec3 blurColor = vec3(0.0);
				float coc = 0.0;
				const int cocBlurSize = 8;
				float cocBlurSizef = float(cocBlurSize);
				const int bokehBlurSize = 8;
				float bokehBlurSizef = float(bokehBlurSize);
				vec2 textureRatio = vec2(1.0/textureWidth, 1.0/textureHeight);
				vec3 sourceColor = texture2D(tColor, vUv).rgb;

				// blur the coc
				float cocBlurWeightTotal = 0.0;
				for(int i=-cocBlurSize; i<cocBlurSize; i++)
                        for(int j=-cocBlurSize; j<cocBlurSize; j++)
                        {
							vec2 dir =  vec2(i, j) * textureRatio;
							float dist = length(dir);
							if(dist > cocBlurSizef)
								continue;
							float weight = 1.0; // getWeight(dist, cocBlurSizef);
							cocBlurWeightTotal += weight;
							vec2 curUv = dir + vUv;
							coc += weight * texture2D(tColor, curUv).a;
						}
				coc /= cocBlurWeightTotal;
				float originalCoc = texture2D(tColor, vUv).a;

				// according to https://developer.nvidia.com/gpugems/GPUGems3/gpugems3_ch28.html
				coc = 2.0 * max(originalCoc, coc) - originalCoc;


				float bokehBlurWeightTotal = 0.0;
				if (coc > 1e-5)
				{
					for(int i=-bokehBlurSize; i<bokehBlurSize; i++)
						for(int j=-bokehBlurSize; j<bokehBlurSize; j++)
						{
							vec2 dir = vec2(i, j) * textureRatio;  // can optimize
							float dist = length(dir);
							if(dist > bokehBlurSizef)
								continue;
							vec2 curUv = dir + vUv;
							float weight = getWeight(dist, bokehBlurSizef);
							bokehBlurWeightTotal += weight;
							blurColor +=  weight * texture2D(tColor, curUv).rgb;
						}
					blurColor /= bokehBlurWeightTotal;

					gl_FragColor.rgb = mix(sourceColor, blurColor, coc);
				}
				else
				{
					gl_FragColor.rgb = sourceColor;
				}
				gl_FragColor.a = 1.0;
			}
		</script>

    <script id="post-vert-1" type="x-shader/x-vertex">
			varying vec2 vUv;

			void main() {
				vUv = uv;
				gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
			}
	</script>
    <script id="post-frag-1" type="x-shader/x-fragment">
            #include <packing>

                varying vec2 vUv;
                uniform sampler2D tColor;
                uniform sampler2D tDepth;
                uniform float cameraNear;
                uniform float cameraFar;
                uniform float focalDepth;
                uniform float farStart;
                uniform float farRange;
                uniform float nearStart;
                uniform float nearRange;
                uniform float textureWidth;
                uniform float textureHeight;

                const int circleSize = 4;		//模糊数值

                float readDepth( sampler2D depthSampler, vec2 coord ) {
                    float fragCoordZ = texture2D( depthSampler, coord ).x;
                    float viewZ = perspectiveDepthToViewZ( fragCoordZ, cameraNear, cameraFar );
                    return viewZ;
				}

				float getWeight(float dist, float maxDist){
					return 1.0 - dist/maxDist;
				}

                vec3 getColorBlur(vec2 uv){
                    vec2 curUv;
                    vec3 color = vec3(0.0, 0.0, 0.0);
                    vec2 textureRatio = vec2(1.0/textureWidth, 1.0/textureHeight);
					float weightTotal = 0.0;
					float circleSizef = float(circleSize);
                    for(int i=-circleSize; i<circleSize; i++)
                        for(int j=-circleSize; j<circleSize; j++)
                        {
							vec2 dir =  vec2(i, j) * textureRatio;
							float dist = length(dir);
                            if(dist > float(circleSize))
                                continue;
                            curUv = uv + dir;
                            float depth = readDepth(tDepth, curUv);

							// background should not mix foregournd in this pass
                            if((-depth - focalDepth - farStart < 0.0))
                            	continue;

							float weight = getWeight(dist, circleSizef);
                            color += weight * texture2D( tColor, curUv ).rgb;
                            weightTotal += getWeight(dist, circleSizef);
                        }
                    return color * (1.0 / weightTotal);
                }

                void main() {
                    vec3 color = texture2D( tColor, vUv ).rgb;
                    float depth = readDepth( tDepth, vUv );

                    float depthVal =  -depth - focalDepth;
                    float coc = 0.0;

                    if(depthVal < 0.0)
                    {
                        coc = (-depthVal - nearStart) / nearRange;
                    }
                    else
                    {
                        coc = (depthVal - farStart) / farRange;
                    }

                    coc = clamp(coc, 0.0, 1.0);
					bool isForeGroundOrFocus = -depth - (focalDepth + farStart) < 0.0;

					// this shader only handle background.
					//  "a" channel stores the coc value.
					if(isForeGroundOrFocus)
					{
						gl_FragColor.rgb = color;
						gl_FragColor.a = coc;
					}
					else
					{
						vec3 colorBlur = getColorBlur(vUv);
						vec3 colorMix = mix(color, colorBlur, coc);
						gl_FragColor.rgb = vec3(colorMix);
					}

                }
		</script>



    <script type="module">
        import { Datas } from './bigdata/Datas.js'
        import { MapChina3d } from './bigdata/MapChina3d.js'

        init();
        function init()
        {
            Datas._gui = new dat.GUI();
            var map = new MapChina3d();

            map.init2();

        }
    </script>




</body>
</html>